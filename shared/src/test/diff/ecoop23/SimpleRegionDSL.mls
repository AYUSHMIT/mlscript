:NewDefs


// * Adapted example from Compositional Embeddings of Domain-Specific Languages (OOPSLA 2022)


// ******************* Initial System *******************

class Vector(x: Int, y: Int)
//│ class Vector(x: Int, y: Int)

class Circle(radius: Int)
class Outside[out Region](a: Region)
class Union[out Region](a: Region, b: Region)
class Intersect[out Region](a: Region, b: Region)
class Translate[out Region](v: Vector, a: Region)
//│ class Circle(radius: Int)
//│ class Outside[Region](a: Region)
//│ class Union[Region](a: Region, b: Region)
//│ class Intersect[Region](a: Region, b: Region)
//│ class Translate[Region](v: Vector, a: Region)

mixin SizeBase {
  fun size(r) =
    if r is
      Circle(_) then 1
      Outside(a) then this.size(a) + 1
      Union(a, b) then this.size(a) + this.size(b) + 1
      Intersect(a, b) then this.size(a) + this.size(b) + 1
      Translate(_, a) then this.size(a) + 1
}
//│ mixin SizeBase() {
//│   this: {size: 'a -> Int}
//│   fun size: (Circle | Intersect['a] | Outside['a] | Translate['a] | Union['a]) -> Int
//│ }

// ******************* Linguistic Reuse and Meta-Language Optimizations *******************

fun round(n: Num): Int = 0
//│ fun round: (n: Num,) -> Int

fun go(x, offset) =
  if x is 0 then Circle(1)
  else
    let shared = go(x - 1, round(offset / 2))
    Union(Translate(Vector(0 - offset, 0), shared), Translate(Vector(offset, 0), shared))
//│ fun go: forall 'Region. (0 | Int & ~0, Int,) -> 'Region
//│   where
//│     'Region :> Circle | Union[Translate['Region]]

// * Note that first-class polymorphism manages (correctly) to preserve the universal quantification
let circles = go(2, 1024)
//│ let circles: forall 'Region. 'Region
//│   where
//│     'Region :> Circle | Union[Translate['Region]]
//│ circles
//│         = Union {}

// ******************* Adding More Language Constructs *******************

class Univ()
class Empty()
class Scale[out Region](v: Vector, a: Region)
//│ class Univ()
//│ class Empty()
//│ class Scale[Region](v: Vector, a: Region)

mixin SizeExt {
  fun size(a) =
    if a is
      Univ then 1
      Empty then 1
      Scale(_, b) then this.size(b) + 1
    else super.size(a)
}
//│ mixin SizeExt() {
//│   super: {size: 'b -> 'c}
//│   this: {size: 'a -> Int}
//│   fun size: (Empty | Object & 'b & ~#Empty & ~#Scale & ~#Univ | Scale['a] | Univ) -> (Int | 'c)
//│ }

module TestSize extends SizeBase, SizeExt
//│ module TestSize {
//│   fun size: 'a -> Int
//│ }
//│ where
//│   'a <: Empty | Object & 'b & ~#Empty & ~#Scale & ~#Univ | Scale['a] | Univ
//│   'b <: Circle | Intersect[Empty | Object & 'b & ~#Empty & ~#Scale & ~#Univ | Scale['a] | Univ] | Outside[Empty | Object & 'b & ~#Empty & ~#Scale & ~#Univ | Scale['a] | Univ] | Translate[Empty | Object & 'b & ~#Empty & ~#Scale & ~#Univ | Scale['a] | Univ] | Union[Empty | Object & 'b & ~#Empty & ~#Scale & ~#Univ | Scale['a] | Univ]

TestSize.size(Empty())
//│ Int
//│ res
//│     = 1

TestSize.size(circles)
//│ Int
//│ res
//│     = 13

TestSize.size(Scale(Vector(1, 1), circles))
//│ Int
//│ res
//│     = 14

// ******************* Adding a New Interpretation *******************
// a stupid power (Int ** Int) implementation
fun pow(x, a) =
  if a is 0 then 1
  else x * pow(x, a - 1)
//│ fun pow: (Int, 0 | Int & ~0,) -> Int

mixin Contains {
  fun contains(a, p) =
    if a is
      Circle(r) then pow(p.x, 2) + pow(p.y, 2) <= pow(r, 2)
      Outside(a) then not (this.contains(a, p))
      Union(lhs, rhs) then this.contains(lhs, p) || this.contains(rhs, p)
      Intersect(lhs, rhs) then this.contains(lhs, p) && this.contains(rhs, p)
      Translate(v, a) then this.contains(a, Vector(p.x - v.x, p.y - v.y))
}
//│ mixin Contains() {
//│   this: {contains: ('a, 'b,) -> Bool & ('a0, Vector,) -> 'c}
//│   fun contains: (Circle | Intersect['a] | Outside['a] | Translate['a0] | Union['a], {x: Int, y: Int} & 'b,) -> (Bool | 'c)
//│ }

module TestContains extends Contains
//│ module TestContains {
//│   fun contains: ('a, {x: Int, y: Int},) -> Bool
//│ }
//│ where
//│   'a <: Circle | Intersect['a] | Outside['a] | Translate['a] | Union['a]

TestContains.contains(Translate(Vector(0, 0), Circle(1)), Vector(0, 0))
//│ Bool
//│ res
//│     = true

TestContains.contains(Intersect(Translate(Vector(0, 0), Circle(1)), Circle(1)), Vector(0, 0))
//│ Bool
//│ res
//│     = true

TestContains.contains(circles, Vector(0, 0))
//│ Bool
//│ res
//│     = false

// ******************* Dependencies, Complex Interpretations, and Domain-Specific Optimizations *******************

fun toString(a: Int): Str = "foo"
fun concat(a: Str, b: Str): Str = a
//│ fun toString: (a: Int,) -> Str
//│ fun concat: (a: Str, b: Str,) -> Str

mixin Text {
  fun text(e) =
    if e is
      Circle(r) then concat("a circular region of radius ", toString(r))
      Outside(a) then concat("outside a region of size ", toString(this.size(a)))
      Union then concat("the union of two regions of size ", toString(this.size(e)))
      Intersect then concat("the intersection of two regions of size ", toString(this.size(e)))
      Translate then concat("a translated region of size ", toString(this.size(e)))
}
//│ mixin Text() {
//│   this: {size: (Intersect[nothing] | Translate['Region] | Union[nothing] | 'a) -> Int}
//│   fun text: (Circle | Intersect[anything] | Outside['a] | Translate['Region] | Union[anything]) -> Str
//│ }

:e
module SizeText extends Text
//│ ╔══[ERROR] Type `#SizeText & {text: forall ?a ?b ?c ?d ?e ?f. ?b -> (?d | ?f | ?a | ?c | ?e)}` does not contain member `size`
//│ ║  l.162: 	      Translate then concat("a translated region of size ", toString(this.size(e)))
//│ ╙──       	                                                                         ^^^^^
//│ ╔══[ERROR] Type `#SizeText & {text: forall ?a ?b ?c ?d ?e ?f. ?e -> (?a | ?b | ?c | ?f | ?d)}` does not contain member `size`
//│ ║  l.161: 	      Intersect then concat("the intersection of two regions of size ", toString(this.size(e)))
//│ ╙──       	                                                                                     ^^^^^
//│ ╔══[ERROR] Type `#SizeText & {text: forall ?a ?b ?c ?d ?e ?f. ?b -> (?e | ?d | ?c | ?a | ?f)}` does not contain member `size`
//│ ║  l.160: 	      Union then concat("the union of two regions of size ", toString(this.size(e)))
//│ ╙──       	                                                                          ^^^^^
//│ ╔══[ERROR] Type `#SizeText & {text: forall ?a ?b ?c ?d ?e ?f. ?e -> (?d | ?f | ?c | ?a | ?b)}` does not contain member `size`
//│ ║  l.159: 	      Outside(a) then concat("outside a region of size ", toString(this.size(a)))
//│ ╙──       	                                                                       ^^^^^
//│ module SizeText {
//│   fun text: (Circle | Intersect[anything] | Outside[anything] | Translate[anything] | Union[anything]) -> Str
//│ }

// * Note: this inferred type got *much worse* after this commit (field access type refinement)
module SizeText extends SizeBase, Text
//│ module SizeText {
//│   fun size: 'a -> Int
//│   fun text: (Circle | Intersect['a] | Outside[Circle | Intersect['a] | Outside['a] | Translate['a] | Union['a]] | Translate['a] | Union['a]) -> Str
//│ }
//│ where
//│   'a <: Circle | Intersect['a] | Outside['a] | Translate['a] | Union['a]

SizeText.text(circles)
//│ Str
//│ res
//│     = 'the union of two regions of size '

SizeText.size(circles)
//│ Int
//│ res
//│     = 13

SizeText.text(Intersect(Translate(Vector(0, 0), Circle(1)), Circle(1)))
//│ Str
//│ res
//│     = 'the intersection of two regions of size '

SizeText.size(Intersect(Translate(Vector(0, 0), Circle(1)), Circle(1)))
//│ Int
//│ res
//│     = 4

mixin IsUniv {
  fun isUniv(e) =
    if e is
      Univ then true
      Outside(a) then this.isEmpty(a)
      Union(a, b) then this.isUniv(a) || this.isUniv(b)
      Intersect(a, b) then this.isUniv(a) && this.isUniv(b)
      Translate(_, a) then this.isUniv(a)
      Scale(_, a) then this.isUniv(a)
    else false
}
//│ mixin IsUniv() {
//│   this: {isEmpty: 'a -> 'b, isUniv: 'a0 -> Bool & 'a1 -> 'b}
//│   fun isUniv: (Intersect['a0] | Object & ~#Intersect & ~#Outside & ~#Scale & ~#Translate & ~#Union & ~#Univ | Outside['a] | Scale['a1] | Translate['a1] | Union['a0] | Univ) -> (Bool | 'b)
//│ }

mixin IsEmpty {
  fun isEmpty(e) =
    if e is
      Univ then true
      Outside(a) then this.isUniv(a)
      Union(a, b) then this.isEmpty(a) || this.isEmpty(b)
      Intersect(a, b) then this.isEmpty(a) && this.isEmpty(b)
      Translate(_, a) then this.isEmpty(a)
      Scale(_, a) then this.isEmpty(a)
    else false
}
//│ mixin IsEmpty() {
//│   this: {isEmpty: 'a -> Bool & 'a0 -> 'b, isUniv: 'a1 -> 'b}
//│   fun isEmpty: (Intersect['a] | Object & ~#Intersect & ~#Outside & ~#Scale & ~#Translate & ~#Union & ~#Univ | Outside['a1] | Scale['a0] | Translate['a0] | Union['a] | Univ) -> (Bool | 'b)
//│ }

module IsUnivIsEmpty extends IsUniv, IsEmpty
//│ module IsUnivIsEmpty {
//│   fun isEmpty: 'a -> (Bool | 'b)
//│   fun isUniv: 'a0 -> (Bool | 'c)
//│ }
//│ where
//│   'a0 <: Intersect['a0] | Object & ~#Intersect & ~#Outside & ~#Scale & ~#Translate & ~#Union & ~#Univ | Outside['a0] | Scale['a0] | Translate['a0] | Union['a0] | Univ
//│   'a <: Intersect['a] | Object & ~#Intersect & ~#Outside & ~#Scale & ~#Translate & ~#Union & ~#Univ | Outside['a] | Scale['a] | Translate['a] | Union['a] | Univ

module IsUnivIsEmpty extends IsEmpty, IsUniv
//│ module IsUnivIsEmpty {
//│   fun isEmpty: 'a -> (Bool | 'b)
//│   fun isUniv: 'a0 -> (Bool | 'c)
//│ }
//│ where
//│   'a0 <: Intersect['a0] | Object & ~#Intersect & ~#Outside & ~#Scale & ~#Translate & ~#Union & ~#Univ | Outside['a0] | Scale['a0] | Translate['a0] | Union['a0] | Univ
//│   'a <: Intersect['a] | Object & ~#Intersect & ~#Outside & ~#Scale & ~#Translate & ~#Union & ~#Univ | Outside['a] | Scale['a] | Translate['a] | Union['a] | Univ

IsUnivIsEmpty.isUniv(circles)
//│ Bool | 'a
//│ res
//│     = false

IsUnivIsEmpty.isEmpty(circles)
//│ Bool | 'a
//│ res
//│     = false

class Foo()
IsUnivIsEmpty.isEmpty(Scale(Vector(1, 2), Intersect(Foo(), circles)))
//│ class Foo()
//│ Bool | 'a
//│ res
//│     = false

mixin Eliminate {
  fun eliminate(e) =
    if e is
      Outside(Outside(a)) then this.eliminate(a)
      Outside(a) then Outside(this.eliminate(a))
      Union(a, b) then Union(this.eliminate(a), this.eliminate(b))
      Intersect(a, b) then Intersect(this.eliminate(a), this.eliminate(b))
      Translate(v, a) then Translate(v, this.eliminate(a))
      Scale(v, a) then Scale(v, this.eliminate(a))
    else e
}
//│ mixin Eliminate() {
//│   this: {eliminate: 'a -> 'b & 'a0 -> 'Region & 'a1 -> 'Region0 & 'a2 -> 'Region1 & 'a3 -> 'Region2 & 'a4 -> 'Region3}
//│   fun eliminate: (Intersect['a2] | Object & 'b & ~#Intersect & ~#Outside & ~#Scale & ~#Translate & ~#Union | Outside['a0 & (Object & ~#Outside | Outside['a])] | Scale['a4] | Translate['a3] | Union['a1]) -> (Intersect['Region1] | Outside['Region] | Scale['Region3] | Translate['Region2] | Union['Region0] | 'b)
//│ }

module TestElim extends Eliminate
//│ module TestElim {
//│   fun eliminate: forall 'Region 'b 'Region0 'c. (Intersect['Region0] | Object & 'b & ~#Intersect & ~#Outside & ~#Scale & ~#Translate & ~#Union | Outside['a & (Object & ~#Outside | Outside['a0])] | Scale['a1] | Translate['a2] | Union['Region]) -> ('d | 'c | 'b)
//│ }
//│ where
//│   'Region0 <: Intersect['Region0] | Object & 'e & ~#Intersect & ~#Outside & ~#Scale & ~#Translate & ~#Union | Outside['a & (Object & ~#Outside | Outside['a0])] | Scale['a1] | Translate['a2] | Union['Region]
//│   'a <: Intersect['Region0] | Object & 'f & ~#Intersect & ~#Outside & ~#Scale & ~#Translate & ~#Union | Outside['a & (Object & ~#Outside | Outside['a0])] | Scale['a1] | Translate['a2] | Union['Region]
//│   'a0 <: Intersect['Region0] | Object & 'd & ~#Intersect & ~#Outside & ~#Scale & ~#Translate & ~#Union | Outside['a & (Object & ~#Outside | Outside['a0])] | Scale['a1] | Translate['a2] | Union['Region]
//│   'a1 <: Intersect['Region0] | Object & 'g & ~#Intersect & ~#Outside & ~#Scale & ~#Translate & ~#Union | Outside['a & (Object & ~#Outside | Outside['a0])] | Scale['a1] | Translate['a2] | Union['Region]
//│   'a2 <: Intersect['Region0] | Object & 'h & ~#Intersect & ~#Outside & ~#Scale & ~#Translate & ~#Union | Outside['a & (Object & ~#Outside | Outside['a0])] | Scale['a1] | Translate['a2] | Union['Region]
//│   'Region <: Intersect['Region0] | Object & 'i & ~#Intersect & ~#Outside & ~#Scale & ~#Translate & ~#Union | Outside['a & (Object & ~#Outside | Outside['a0])] | Scale['a1] | Translate['a2] | Union['Region]
//│   'd :> 'c
//│   'c :> Outside['d | 'c | 'f] | Union['d | 'c | 'i] | Intersect['d | 'c | 'e] | Translate['d | 'c | 'h] | Scale['d | 'c | 'g]

TestElim.eliminate(Outside(Outside(Univ())))
//│ forall 'a. 'b | 'a
//│   where
//│     'b :> forall 'a. Univ | 'a
//│     'a :> Outside[forall 'a. Univ | 'b | 'a] | Union[forall 'a. 'b | 'a] | Intersect[forall 'a. 'b | 'a] | Translate[forall 'a. 'b | 'a] | Scale[forall 'a. 'b | 'a]
//│ res
//│     = Univ {}

TestElim.eliminate(circles)
//│ forall 'a. Circle | 'b | 'a
//│   where
//│     'b :> forall 'a. 'a
//│     'a :> Outside[forall 'a. 'b | 'a] | Union[forall 'a. 'b | 'a] | Intersect[forall 'a. 'b | 'a] | Translate[forall 'a. Circle | 'b | 'a] | Scale[forall 'a. 'b | 'a]
//│ res
//│     = Union {}

fun mk(n) = if n is
  1 then Outside(mk(n))
  2 then Union(mk(n), mk(n))
  3 then Intersect(mk(n), mk(n))
  4 then Translate(Vector(0, 0), mk(n))
  _ then Scale(Vector(0, 0), mk(n))
//│ fun mk: forall 'Region. Object -> 'Region
//│   where
//│     'Region :> Intersect['Region] | Outside['Region] | Scale['Region] | Translate['Region] | Union['Region]

:re
TestElim.eliminate(mk(100))
//│ forall 'a. 'b | 'a
//│   where
//│     'b :> forall 'a. 'a
//│     'a :> Outside[forall 'a. 'b | 'a] | Union[forall 'a. 'b | 'a] | Intersect[forall 'a. 'b | 'a] | Translate[forall 'a. 'b | 'a] | Scale[forall 'a. 'b | 'a]
//│ res
//│ Runtime error:
//│   RangeError: Maximum call stack size exceeded

// *************************************************************************

module Lang extends SizeBase, SizeExt, Contains, Text, IsUniv, IsEmpty, Eliminate
//│ module Lang {
//│   fun contains: ('a, {x: Int, y: Int},) -> Bool
//│   fun eliminate: forall 'Region 'b 'c 'Region0. (Intersect['Region] | Object & 'c & ~#Intersect & ~#Outside & ~#Scale & ~#Translate & ~#Union | Outside['a0 & (Object & ~#Outside | Outside['a1])] | Scale['a2] | Translate['a3] | Union['Region0]) -> ('d | 'b | 'c)
//│   fun isEmpty: 'a4 -> (Bool | 'e)
//│   fun isUniv: 'a5 -> (Bool | 'f)
//│   fun size: 'Region1 -> Int
//│   fun text: 'g -> Str
//│ }
//│ where
//│   'g <: Circle | Intersect['Region2] | Outside[Empty | Object & 'g & ~#Empty & ~#Scale & ~#Univ | Scale['Region2] | Univ] | Translate['Region2] | Union['Region2]
//│   'Region2 <: Empty | Object & 'g & ~#Empty & ~#Scale & ~#Univ | Scale['Region2] | Univ
//│   'Region1 <: Empty | Object & 'h & ~#Empty & ~#Scale & ~#Univ | Scale['Region1] | Univ
//│   'h <: Circle | Intersect['Region1] | Outside[Empty | Object & 'h & ~#Empty & ~#Scale & ~#Univ | Scale['Region1] | Univ] | Translate['Region1] | Union['Region1]
//│   'a5 <: Intersect['a5] | Object & ~#Intersect & ~#Outside & ~#Scale & ~#Translate & ~#Union & ~#Univ | Outside['a5] | Scale['a5] | Translate['a5] | Union['a5] | Univ
//│   'a4 <: Intersect['a4] | Object & ~#Intersect & ~#Outside & ~#Scale & ~#Translate & ~#Union & ~#Univ | Outside['a4] | Scale['a4] | Translate['a4] | Union['a4] | Univ
//│   'Region <: Intersect['Region] | Object & 'i & ~#Intersect & ~#Outside & ~#Scale & ~#Translate & ~#Union | Outside['a0 & (Object & ~#Outside | Outside['a1])] | Scale['a2] | Translate['a3] | Union['Region0]
//│   'a0 <: Intersect['Region] | Object & 'j & ~#Intersect & ~#Outside & ~#Scale & ~#Translate & ~#Union | Outside['a0 & (Object & ~#Outside | Outside['a1])] | Scale['a2] | Translate['a3] | Union['Region0]
//│   'a1 <: Intersect['Region] | Object & 'd & ~#Intersect & ~#Outside & ~#Scale & ~#Translate & ~#Union | Outside['a0 & (Object & ~#Outside | Outside['a1])] | Scale['a2] | Translate['a3] | Union['Region0]
//│   'a2 <: Intersect['Region] | Object & 'k & ~#Intersect & ~#Outside & ~#Scale & ~#Translate & ~#Union | Outside['a0 & (Object & ~#Outside | Outside['a1])] | Scale['a2] | Translate['a3] | Union['Region0]
//│   'a3 <: Intersect['Region] | Object & 'l & ~#Intersect & ~#Outside & ~#Scale & ~#Translate & ~#Union | Outside['a0 & (Object & ~#Outside | Outside['a1])] | Scale['a2] | Translate['a3] | Union['Region0]
//│   'Region0 <: Intersect['Region] | Object & 'm & ~#Intersect & ~#Outside & ~#Scale & ~#Translate & ~#Union | Outside['a0 & (Object & ~#Outside | Outside['a1])] | Scale['a2] | Translate['a3] | Union['Region0]
//│   'd :> 'b
//│   'b :> Outside['d | 'b | 'j] | Union['d | 'b | 'm] | Intersect['d | 'b | 'i] | Translate['d | 'b | 'l] | Scale['d | 'b | 'k]
//│   'a <: Circle | Intersect['a] | Outside['a] | Translate['a] | Union['a]

Lang.size(circles)
//│ Int
//│ res
//│     = 13

Lang.contains(circles, Vector(0, 0))
//│ Bool
//│ res
//│     = false

Lang.text(circles)
//│ Str
//│ res
//│     = 'the union of two regions of size '

Lang.isUniv(circles)
//│ Bool | 'a
//│ res
//│     = false

Lang.isEmpty(circles)
//│ Bool | 'a
//│ res
//│     = false

Lang.size(Lang.eliminate(circles))
//│ Int
//│ res
//│     = 13

:re
Lang.size(mk(100))
//│ Int
//│ res
//│ Runtime error:
//│   RangeError: Maximum call stack size exceeded

:e
:re
Lang.contains(mk(100), Vector(0, 0))
//│ ╔══[ERROR] Type mismatch in application:
//│ ║  l.416: 	Lang.contains(mk(100), Vector(0, 0))
//│ ║         	^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^
//│ ╟── application of type `Scale[?Region]` does not match type `Circle | Intersect[?Region0] | Outside[?Region1] | Translate[?Region2] | Union[?Region3]`
//│ ║  l.334: 	  _ then Scale(Vector(0, 0), mk(n))
//│ ║         	         ^^^^^^^^^^^^^^^^^^^^^^^^^^
//│ ╟── Note: constraint arises from reference:
//│ ║  l.114: 	    if a is
//│ ║         	       ^
//│ ╟── Note: type parameter Region is defined at:
//│ ║  l.16: 	class Translate[out Region](v: Vector, a: Region)
//│ ╙──      	                    ^^^^^^
//│ error
//│ res
//│ Runtime error:
//│   RangeError: Maximum call stack size exceeded

:e
:re
Lang.text(mk(100))
//│ ╔══[ERROR] Type mismatch in application:
//│ ║  l.436: 	Lang.text(mk(100))
//│ ║         	^^^^^^^^^^^^^^^^^^
//│ ╟── application of type `Scale[?Region]` does not match type `Circle | Intersect[?Region0] | Outside[?Region1] | Translate[?Region2] | Union[?Region3]`
//│ ║  l.334: 	  _ then Scale(Vector(0, 0), mk(n))
//│ ║         	         ^^^^^^^^^^^^^^^^^^^^^^^^^^
//│ ╟── but it flows into application with expected type `Circle | Intersect[?Region4] | Outside[?Region5] | Translate[?Region6] | Union[?Region7]`
//│ ║  l.436: 	Lang.text(mk(100))
//│ ║         	          ^^^^^^^
//│ ╟── Note: constraint arises from reference:
//│ ║  l.157: 	    if e is
//│ ╙──       	       ^
//│ Str | error
//│ res
//│ Runtime error:
//│   RangeError: Maximum call stack size exceeded

:re
Lang.isUniv(mk(100))
//│ Bool | 'a
//│ res
//│ Runtime error:
//│   RangeError: Maximum call stack size exceeded

:re
Lang.isEmpty(mk(100))
//│ Bool | 'a
//│ res
//│ Runtime error:
//│   RangeError: Maximum call stack size exceeded

:re
Lang.size(Lang.eliminate(mk(100)))
//│ Int
//│ res
//│ Runtime error:
//│   RangeError: Maximum call stack size exceeded
