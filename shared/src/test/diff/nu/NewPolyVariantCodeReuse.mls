:NewParser
:NewDefs
:NoJS

// Adapted example from Code reuse through polymorphic variants (FOSE 2000)
class Nil
//│ class Nil()

class Cons<A>(head: A, tail: Cons<A> | Nil)
//│ class Cons[A](head: A, tail: Cons[A] | Nil)

let l = Cons(1, Nil())
//│ let l: Cons['A]
//│   where
//│     'A :> 1

class NotFound
class Success<A>(result: A)
//│ class NotFound()
//│ class Success[A](result: A)

fun eq(l: string, r: string): bool
//│ fun eq: (l: string, r: string,) -> bool

fun list_assoc(s, l) =
  if l is
    Cons(h, t) then
      if eq(s, h._1) then Success(h._2)
      else list_assoc(s, t)
    Nil then NotFound()
//│ fun list_assoc: (string, Cons['A] | Nil,) -> (NotFound | Success['A0])
//│   where
//│     'A <: {_1: string, _2: 'A0}

// fun list_assoc(s: string, l: Cons<{ _1: string, _2: 'b }> | Nil): NotFound | Success['b]

class Var(s: string)
//│ class Var(s: string)

mixin EvalVar {
  fun eval(sub, v) =
    if v is Var(s) then
      if list_assoc(s, sub) is
        NotFound then v
        Success(r) then r
}
//│ mixin EvalVar() {
//│   fun eval: (Cons['A] | Nil, Var,) -> (Var | 'result)
//│ }
//│ where
//│   'A <: {_1: string, _2: 'result}

class Abs<A>(x: string, t: A)
class App<A>(s: A, t: A)
//│ class Abs[A](x: string, t: A)
//│ class App[A](s: A, t: A)

fun incr(x: {a: int}): unit
//│ fun incr: (x: {a: int},) -> unit

fun gensym(): string
//│ fun gensym: () -> string

fun int_to_string(x: int): string
//│ fun int_to_string: (x: int,) -> string

mixin EvalLambda {
  fun eval(sub, v) =
    if v is
      App(t1, t2) then
        let l1 = this.eval(sub, t1)
        let l2 = this.eval(sub, t2)
        if t1 is
          Abs(x, t) then this.eval(Cons((x, l2), Nil()), t)
        else
          App(l1, l2)
      Abs(x, t) then
        let s = gensym()
        Abs(s, this.eval(Cons((x, Var(s)), sub), t))
    else
      super.eval(sub, v)
}
//│ mixin EvalLambda() {
//│   super: {eval: ('a, 'b,) -> 'c}
//│   this: {eval: (Cons[in 'A out (string, 'd,) | 'A], 't,) -> 'c & ('a, 't0,) -> ('d & 'e) & (Cons[in 'A0 out (string, Var,) | 'A0 | 'A1], 't1,) -> 'f}
//│   fun eval: ('a & (Cons['A2] | Nil), Abs['t1] | App['t0 & (Abs['t] | ~Abs)] | 'b & ~Abs & ~App,) -> (Abs['f] | App['d | 'e] | 'c)
//│ }
//│ where
//│   'A2 :> (string, Var,) | 'A0
//│       <: 'A1

// def eval_lambda eval_rec subst v = case v of {
//   | Var -> eval_var subst v
//   | App -> let l2 = eval_rec subst v.lhs in
//     let l1 = eval_rec subst v.rhs in
//     case l1 of {
//     | Abs -> eval_rec (Cons (Tuple l1.name l2) Nil) l1.body
//     | _ -> App { lhs = l1; rhs = l2 }
//     }
//   | Abs -> let new_name = int_to_string ((gensym ())._2.a) in
//     Abs { name = new_name;
//           body = eval_rec (Cons (Tuple v.name (Var { name = new_name })) subst) v.body }
//   }

module Test1: EvalVar, EvalLambda
//│ module Test1() {
//│   fun eval: (Cons['A | 'A0] | Nil, 'a,) -> 'result
//│ }
//│ where
//│   'a <: Abs['a] | App['a & (Abs['a] | ~Abs)] | Var
//│   'A :> 'A0
//│      <: 'A0 & 'A1
//│   'A0 :> (string, 'result,)
//│       <: 'A1 & {_1: string, _2: 'result}
//│   'A1 <: 'A & 'A0
//│   'result :> Var | App['result] | Abs['result]

