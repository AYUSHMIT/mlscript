class None: {}
class Some[A]: { v: A }
//│ Defined class None
//│ Defined class Some

def Some v = Some{v}
def None = None{}
//│ Some: 'a -> (some & {Some#A :> 'A <: 'A | 'a, v: 'a})
//│ None: none

def flatMap = fun f -> fun opt ->
  case opt of { Some -> f opt.v | _ -> opt }
//│ flatMap: ('a -> 'b) -> (some & {v: 'a} | 'b & ~some) -> 'b

f x = Some x
flatMap f (Some 1)
flatMap f None
flatMap f "oops"
//│ f: 'a -> (some & {Some#A :> 'A <: 'A | 'a, v: 'a})
//│ res: some & {Some#A :> 'A <: 1 | 'A, v: 1}
//│ res: none | some & {Some#A = 'A, v: nothing}
//│ res: "oops" | some & {Some#A = 'A, v: nothing}

class NoneBecause: { reason: string }
flatMap f (NoneBecause { reason = "uh uh" })
//│ Defined class NoneBecause
//│ res: noneBecause & {reason: "uh uh"} | some & {Some#A = 'A, v: nothing}

type Expr = Lit | Neg | Var | Plus
class Lit: {v: int}
class Neg: {sub: Expr}
class Var: {nme: string}
class Plus: {lhs: Expr; rhs: Expr}
//│ Defined type alias Expr
//│ Defined class Lit
//│ Defined class Neg
//│ Defined class Var
//│ Defined class Plus

rec def evalOpt x = case x of {
  | Lit ->
      Some x.v
  | Neg ->
      // flatMap (fun s -> Some (neg x.sub)) (evalOpt x.sub)
      flatMap (fun s -> Some (0 - s)) (evalOpt x.sub)
  | Var ->
      // None with {err = concat "free var: " x.nme}
      NoneBecause {reason = concat "free var: " x.nme}
      // None
  | Plus ->
      flatMap (fun l -> flatMap (fun r ->
        // Some (add x.lhs x.rhs)
        Some (l + r)
      ) (evalOpt x.rhs)) (evalOpt x.lhs)
  }
//│ evalOpt: (lit & {v: int & 'b} | neg & {sub: 'a} | plus & {lhs: 'a, rhs: 'a} | var & {nme: string} as 'a) -> (noneBecause & {reason: string} | some & {Some#A :> 'A <: 'A | 'b, v: 'b} | some & {Some#A :> 'A0 <: 'A0 | int, v: int} | some & {Some#A :> 'A1 <: 'A1 | int, v: int})

evalOpt (Plus{lhs = Lit{v=2}; rhs = Lit{v=3}})
//│ res: noneBecause & {reason: string} | some & {Some#A :> 'A <: 2 | 3 | 'A, v: 2 | 3} | some & {Some#A :> 'A0 <: 'A0 | int, v: int} | some & {Some#A :> 'A1 <: 'A1 | int, v: int}
